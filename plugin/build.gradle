plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    implementation project(':worldeditselectionvisualizer-compatibility-commons')
    implementation project(':worldeditselectionvisualizer-compatibility-v6')
    implementation project(':worldeditselectionvisualizer-compatibility-v7')
    implementation 'fr.mrmicky:FastParticles:1.2.4'
    implementation 'org.bstats:bstats-bukkit:1.8'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.2.0'
    compileOnly 'com.intellectualsites.fawe:FAWE-Bukkit:1.16-326'
    compileOnly 'me.clip:placeholderapi:2.10.9'
}

shadowJar {
    archiveFileName = "WorldEditSelectionVisualizer-${project.version}.jar"

    relocate 'org.bstats.bukkit', 'fr.mrmicky.worldeditselectionvisualizer.metrics'
    relocate 'fr.mrmicky.fastparticle', 'fr.mrmicky.worldeditselectionvisualizer.libs.fastparticles'

    exclude 'org/intellij/lang/annotations/**'
}

artifacts {
    archives shadowJar
}

processResources {
    filesMatching('**/*.yml') {
        expand 'pluginVersion': project.version
    }
}
